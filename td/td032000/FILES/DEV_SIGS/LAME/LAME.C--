// Радость Коллекционера v.1.0 :)

// Я не знаю,но с моим алгоритмом воссоздания сигнатур,требуется минимальный
// размер сигнатуры равный 12 байтам. У Януша Миловского получилось меньше (?)
// (как не знаю - IV#15 не читал).
// В результате вместо обьявленных 17??? сигнатур,нормально генерироватся будут
// только около 12 тысяч. (я до конца никогда не дожидался - мне и 3000 хватало)

?resize	FALSE				// Некоторые описания для компилятора

byte	xsig[16];			// Для сигнатуры
byte	free[2000];			// Свободное пространство

void    makesig()			// Воссоздает сигнатуру
{
	$lodsb;	CL = AL;		// Берем первый байт сигнатуры
	$lodsw;
	if (AX>0x8000) return();	// > 32767 ? да ! на выход
	BX = #free+AX;			// lea bx,[free][ax] :)
	DSBYTE[BX]   = CL;		// Байт сигнатуры
	AX = 0;	DX = 0;	AL = CL;	// AX = DX = 0; AL = байт сигнатуры
	AX><DX;
	$xor	DH,DL
	DH><DL;	AH><AL;
	DSWORD[BX+4]	=	DX;	// Пропатчим сигнатуру
	DSWORD[BX+6]	=	AX;

        AX = 0;
	$lodsb;         DI = AX;	// Возьмем размер сигнатуры

	$lodsw
	$push	AX			// Возьмем саму сигнатуру
	$lodsw
	AX><DX;
	$pop	AX

	$xor	AL,DH			// Произведем манипуляции
	$xor	AH,DL
	$xor	AH,DH
	AH><AL;		DH><DL;
        $push SI
	DSWORD[BX+DI-4]	=	DX;	// Сохраним
	DSWORD[BX+DI-2]	=	AX;
        $pop  SI
}

char	fname	=	"00000000.vir";	// Собственно файл-маск

main()
word	hndl;
{
	hndl = FOPEN(0,,,"sig.out");	// Откроем сигнатуры
	while (FREAD(,hndl,16,#xsig) != 0) {

	$cld;
	DI = #free;	CX = 2000;	AX = 0;	$rep;	$stosb
	SI = #xsig;			// Очистим буффер

	if ((xsig[3] >= 12) && (xsig[3+8] >= 12))
	{				// Сигнатура подходит ?
        makesig();
        makesig();			// Создаем оную

	BX = FCREATE(,,0,#fname);
	FWRITE(,BX,2000,#free);		// Создаем файл
	FCLOSE(,BX);			// пишим туда псевдо-вирус

	CX = 6;	SI = #fname+7;
	DSBYTE[SI]++;
	loop(CX)
	{				// Разберемся с именем
	if (char DSBYTE[SI] > '9')
				{
				DSBYTE[SI]   = byte '0';
				DSBYTE[SI-1]++;
				}
	SI--;
	}
	}
	}
	FCLOSE(,hndl);			// Закрыли файл сигнатур
}