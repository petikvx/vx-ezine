@ECHO OFF
IF NOT "%OS%"=="Windows_NT" GOTO Syntax
IF NOT [%1]==[] GOTO Syntax

:: Create temporary debug script
>  "%Temp%.\biosdate.dbg" ECHO D FFFF:0005 L 8
>> "%Temp%.\biosdate.dbg" ECHO Q

:: Use temporary debug script to retrieve BIOS date
FOR /F "tokens=9" %%A IN ('DEBUG ^< "%Temp%.\biosdate.dbg" ^| FIND "/"') DO SET BIOSDate=%%A

:: Remove temporary debug script
DEL "%Temp%.\biosdate.dbg"

:: Diplay result
SET BIOSDate

:: Done
GOTO:EOF


:Syntax
ECHO.
ECHO BIOSDate.cmd,  Version 1.00 for Windows NT 4 / 2000 / XP
ECHO Displays the BIOS date for the local computer
ECHO.
ECHO Usage:  BIOSDATE
ECHO.
ECHO Written by Rob van der Woude
ECHO http://www.robvanderwoude.com
ECHO.
ECHO Original idea by ComputerHope
ECHO http://www.computerhope.com/rdebug.htm
