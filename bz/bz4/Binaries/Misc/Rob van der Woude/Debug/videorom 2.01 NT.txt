@ECHO OFF
SETLOCAL ENABLEEXTENSIONS

SET Single=0
IF "%1"=="/1" SET Single=1
IF NOT "%1"=="" IF NOT "%1"=="/1" GOTO Syntax

SET FullParam=
SET DebugScript=%Temp%.\%~n0.dbg
(ECHO d C000:0040) >  %debugscript%
(ECHO d C000:00C0) >> %debugscript%
(ECHO q)           >> %DebugScript%
ECHO.
FOR /F "tokens=*" %%a IN ('DEBUG ^< %DebugScript%') DO CALL :Subroutine %%a
DEL %DebugScript%
IF %Single%==1 ECHO.%FullParam%
GOTO End

:Subroutine
FOR /F "tokens=1 delims=: " %%A IN ("%1") DO IF NOT "%%A"=="C000" GOTO:EOF
SET Param=%*
SET Param=%Param:~61%
VER | FIND "Windows NT" >NUL
IF NOT ERRORLEVEL 1 SET Param=%Param:~1%
SET Param=%Param:(=^(%
SET FullParam=%FullParam%%Param%
IF NOT %Single%==1 ECHO.%Param%
GOTO:EOF

:Syntax
ECHO.
ECHO VideoROM.cmd,  Version 2.01 for Windows NT 4 / 2000
ECHO Reads and displays manufacturer information from your video adapter ROM
ECHO.
ECHO Usage:  %~n0  [ /1 ]
ECHO         /1 switch results in single line output
ECHO            ^(will be wrapped on screen though^)
ECHO            default is 16 lines of 16 characters each
ECHO.
ECHO Written by Rob van der Woude
ECHO http://www.robvanderwoude.com
ECHO Idea by ComputerHope
ECHO http://www.computerhope.com/rdebug.htm

:End
ENDLOCAL
