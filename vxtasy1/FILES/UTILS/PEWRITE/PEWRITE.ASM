;께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께
; PEWrite V1.0
;
; Portable Executable Code Section Write Enable
;
; (C) Lord Julus 1999
;
; (lordjulus@geocities.com)
;
;께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께
.586p                                  ;
.model flat, stdcall                   ;
                                       ;
extrn ExitProcess:proc                 ;
extrn GetCommandLineA:proc             ;
extrn CreateFileA:proc                 ;
extrn WriteFile: proc                  ;
extrn GetStdHandle: proc               ;
extrn CloseHandle:proc                 ;
extrn GetFileSize:proc                 ;
extrn CreateFileMappingA:proc          ;
extrn MapViewOfFile:proc               ;
extrn UnmapViewOfFile:proc             ;
extrn SetFilePointer:proc              ;
extrn SetEndOfFile:proc                ;
                                       ;
include w32nt_lj.inc                   ;
                                       ;
.data                                  ;
handle           dd 0                  ;
maphandle        dd 0                  ;
mapaddress       dd 0                  ;
filesize         dd 0                  ;
peheader         dd 0                  ;
numberofsections dw 0                  ;
baseofcode       dd 0                  ;
text             db 13,10,"PEWrite V1.0 by Lord Julus (C) 1999",13,10
text_len =       $-offset text         ;
error            db 13,10,"Error processing specified file!",13,10
error_len =      $-offset error        ;
temp             dd 0                  ;
console          dd 0                  ;
;께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께
.code                                  ;
start:                                 ;
        push 0FFFFFFF5h                ;
        call GetStdHandle              ;
        mov console, eax               ;
                                       ;
        or eax, eax                    ;
        jz invalid_console             ;
        push 0                         ;
        push offset temp               ;
        push text_len                  ;
        push offset text               ;
        push eax                       ;
        call WriteFile                 ;
                                       ;
invalid_console:                       ;
        call GetCommandLineA           ;
                                       ;
        cmp eax, 0                     ;
        je open_error                  ;
                                       ;
        mov edi, eax                   ;
        xor eax, eax                   ;
        mov al, '.'                    ;
        mov ecx, 260                   ;
        repnz scasb                    ;
        add edi, 2                     ;
                                       ;
scan_2:                                ;
        inc edi                        ;
        cmp byte ptr [edi], 0          ;
        je open_error                  ;
        cmp byte ptr [edi], '"'        ;
        je scan_2                      ;
        cmp byte ptr [edi], 20h        ;
        jne found_cmd                  ;
        jmp scan_2                     ;
                                       ;
found_cmd:                             ;
        mov esi, edi                   ;
                                       ;
        push 0                         ;
        push 0                         ;
        push OPEN_EXISTING             ;
        push 0                         ;
        push 0                         ;
        push GENERIC_READ or GENERIC_WRITE;
        push esi                       ;
        Call CreateFileA               ;
        cmp eax, -1                    ;
        je open_error                  ;
        mov [handle], eax              ;
                                       ;
        push 0                         ;
        push handle                    ;
        call GetFileSize               ;
        cmp eax, -1                    ;
        je close_file                  ;
        mov [filesize], eax            ;
                                       ;
        push 0                         ;
        push eax                       ;
        push 0                         ;
        push PAGE_READWRITE            ;
        push 0                         ;
        push handle                    ;
        call CreateFileMappingA        ;
                                       ;
        cmp eax, 0                     ;
        je close_file                  ;
        mov [maphandle], eax           ;
                                       ;
        push 0                         ;
        push 0                         ;
        push 0                         ;
        push FILE_MAP_ALL_ACCESS       ;
        push eax                       ;
        call MapViewOfFile             ;
        cmp eax, 0                     ;
        je close_map                   ;
        mov [mapaddress], eax          ;
                                       ;
        call Enable_Write              ;
                                       ;
        push mapaddress                ;
        call UnmapViewOfFile           ;
                                       ;
close_map:                             ;
        push maphandle                 ;
        call CloseHandle               ;
                                       ;
close_file:                            ;
        push FILE_BEGIN                ;
        push 0                         ;
        push filesize                  ;
        push handle                    ;
        call SetFilePointer            ;
                                       ;
        push handle                    ;
        call SetEndOfFile              ;
                                       ;
        push handle                    ;
        call CloseHandle               ;
        jmp quit                       ;
                                       ;
open_error:                            ;
        mov eax, console               ;
        or eax, eax                    ;
        jz quit                        ;
        push 0                         ;
        push offset temp               ;
        push error_len                 ;
        push offset error              ;
        push eax                       ;
        call WriteFile                 ;
                                       ;
quit:                                  ;
        push 0                         ;
        call ExitProcess               ;
;께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께
Enable_Write:                          ;
        mov esi, mapaddress            ;
        cmp word ptr [esi], 'ZM'       ;
        jne exit                       ;
        mov eax, [esi.MZ_lfanew]       ;
        cmp eax, 1000h                 ;
        ja exit                        ;
        add eax, mapaddress            ;
        mov esi, eax                   ;
        mov peheader, esi              ;
        cmp word ptr [esi], 'EP'       ;
        jne exit                       ;
        mov ax, [esi.NumberOfSections] ;
        mov numberofsections, ax       ;
                                       ;
        add esi, IMAGE_FILE_HEADER_SIZE;
        mov eax, [esi.OH_BaseOfCode]   ;
        mov baseofcode, eax            ;
                                       ;
        mov eax, peheader              ;
        xor ebx, ebx                   ;
        mov bx, word ptr [eax.SizeOfOptionalHeader]
        mov esi, peheader              ;
        add esi, IMAGE_FILE_HEADER_SIZE;
        add esi, ebx                   ;
                                       ;
        xor ecx, ecx                   ;
        mov cx, numberofsections       ;
                                       ;
locate_section:                        ;
        mov eax, [esi.SH_VirtualAddress]
        cmp eax, baseofcode            ;
        je found                       ;
                                       ;
        add esi, IMAGE_SIZEOF_SECTION_HEADER
        loop locate_section            ;
        jmp exit                       ;
                                       ;
found:                                 ;
        mov eax, [esi.SH_Characteristics]
        or eax, IMAGE_SCN_MEM_WRITE    ;
        mov [esi.SH_Characteristics], eax
                                       ;
exit:                                  ;
        ret                            ;
                                       ;
end start                              ;
end                                    ;
;께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께께
